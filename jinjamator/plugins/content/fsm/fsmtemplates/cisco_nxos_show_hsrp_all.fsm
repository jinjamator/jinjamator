# Object names are based on pyATS/Genie parser (boolean values are left out)
Value INTERFACE (\S+)
Value VERSION (\d+)
Value IP_VERSION (\d)
Value GROUP_NUMBER (\d+)
Value PRIORITY (\d+)
Value HSRP_ROUTER_STATE (.+)
Value CONFIGURED_PRIORITY (\d+)
Value PREEMPT (.*)
Value PREEMPT_DELAY_MINIMUM (\d+)
Value PREEMPT_DELAY_RELOAD (\d+)
Value PREEMPT_DELAY_SYNC (\d+)
Value LOWER_FWD_TRESHOLD (\d+)
Value UPPER_FWD_TRESHOLD (\d+)
# Value TIMERS_HELLO_MSEC_FLAG
# Value TIMERS_HOLD_MSEC_FLAG
Value TIMERS_HELLO_SEC (\d+)
Value TIMERS_HOLD_SEC (\d+)
Value PRIMARY_IP_ADDRESS (\S+)
# Value VIRTUAL_IP_LEARN
Value List SECONDARY_IP_ADDRESS (\S+)
Value ACTIVE_ROUTER (\S+)
Value ACTIVE_EXPIRE (\d+\.\d+)
Value ACTIVE_IP_ADDRESS (\S+)
Value ACTIVE_PRIORITY (\d+)
Value STANDBY_ROUTER (\S+)
Value STANDBY_EXPIRE (\d+\.\d+)
Value STANDBY_IP_ADDRESS (\S+)
Value STANDBY_PRIORITY (\d+)
Value AUTHENTICATION_MD5_KEY_STRING (\S+)
Value AUTHENTICATION_MD5_KEY_CHAIN (\S+)
Value AUTHENTICATION_TEXT (\S+)
Value VIRTUAL_MAC_ADDRESS (\S+)
Value VIRTUAL_MAC_ADDRESS_STATUS (.*)
Value NUM_STATE_CHANGES (\d+)
Value LAST_STATE_CHANGE (\S+)
Value SESSION_NAME (\S+)
Value SESSION_NAME_TYPE (\S+)
Value BFD_CONFIG_STATE (\S+)
Value BFD_SESSION_STATE (\S+)
Value HSRP_MASTER_GROUP (\S+)


Start
  ^${INTERFACE}\s+-\s+Group\s${GROUP_NUMBER}\s+\(HSRP-V${VERSION}\)\s\(IPv${IP_VERSION}\)\s*$$
  ^\s+Local\s+state\s+is\s+${HSRP_ROUTER_STATE},\s+priority\s+${PRIORITY}\s+\(Cfged\s+${CONFIGURED_PRIORITY}\)(?:,\s+${PREEMPT})?.*$$  
  ^\s+.*lower:\s+${LOWER_FWD_TRESHOLD}\s+upper:\s+${UPPER_FWD_TRESHOLD}\s*$$
  #^\s+Preemption\s+Delay\s+\(Seconds\)\s+(?:Minimum\:${PREEMPT_DELAY_MINIMUM})(?:Reload\:${PREEMPT_DELAY_RELOAD})(?:Sync\:${PREEMPT_DELAY_SYNC}).*$$
  #^\s+Preemption\s+Delay\s+\(Seconds\)(?:\s+Minimum\:${PREEMPT_DELAY_MINIMUM})(?:\s+Reload:${PREEMPT_DELAY_RELOAD})(?:\s+Sync:${PREEMPT_DELAY_SYNC}).*$$
  ^\s+Preemption\s+Delay\s+\(Seconds\) ((Reload:(${PREEMPT_DELAY_RELOAD})\s*)|(Sync:(${PREEMPT_DELAY_SYNC})\s*)|(Minimum:(${PREEMPT_DELAY_MINIMUM})\s*)){1,3}.*$$
  ^\s+Hellotime\s+${TIMERS_HELLO_SEC}\s+sec,\s+holdtime\s+${TIMERS_HOLD_SEC}\s+sec.*$$
  ^\s+Next\s+hello\s+sent.*$$
  ^\s+Virtual\s+IP\s+address\s+is\s+${PRIMARY_IP_ADDRESS}\s+.*$$
  ^\s+Secondary\s+Virtual\s+IP\s+address\s+is\s+${SECONDARY_IP_ADDRESS}\s*$$
  ^\s+Active\s+router\s+is\s+${ACTIVE_ROUTER}\s*$$
  ^\s+Active\s+router\s+is\s+${ACTIVE_IP_ADDRESS}\s?,\s+priority\s+${ACTIVE_PRIORITY}\s+expires\s+in\s+${ACTIVE_EXPIRE}.*$$
  ^\s+Standby\s+router\s+is\s+${STANDBY_ROUTER}\s*$$
  ^\s+Standby\s+router\s+is\s+${STANDBY_IP_ADDRESS}\s?,\s+priority\s+${STANDBY_PRIORITY}\s+expires\s+in\s+${STANDBY_EXPIRE}.*$$
  ^\s+Authentication\s+MD5,\s+key-string\s+"${AUTHENTICATION_MD5_KEY_STRING}".*$$
  ^\s+Authentication\s+MD5,\s+key-chain\s+${AUTHENTICATION_MD5_KEY_CHAIN}.*$$
  ^\s+Authentication\s+text\s+"${AUTHENTICATION_TEXT}".*$$
  ^\s+BFD\s+${BFD_CONFIG_STATE}\s+Session\s+${BFD_SESSION_STATE}.*$$
  ^\s+Virtual\s+mac\s+address\s+is\s+${VIRTUAL_MAC_ADDRESS} \(${VIRTUAL_MAC_ADDRESS_STATUS}\)\s*$$
  ^\s+${NUM_STATE_CHANGES}\s+state\s+changes,\s+last\s+state\s+change\s+${LAST_STATE_CHANGE}\s*$$
  ^\s+Slave to master: ${HSRP_MASTER_GROUP}\s.*$$
  ^\s+IP\s+redundancy\s+name\s+is\s+${SESSION_NAME}\s+\(${SESSION_NAME_TYPE}\).*$$
  ^\s+Track.*$$
  ^\s+Secondary VIP.*$$ -> Secondary_VIPs
  ^\s+\(Ignoring MD5, using text authentication, as this is V1\).*$$
  # Record data when empty line is found
  ^\s*$$ -> Record
  ^. -> Error

Secondary_VIPs
  #^\s+\S+:\S*:\S+.*$$
  # Catch secondary IPV4-VIPs:
  ^\s+\d+\.\d+\.\d+\.\d+$$
  #Ignore everything
  ^\s+${SECONDARY_IP_ADDRESS}.*$$
  #Jump back to Start for empty-line
  ^\s*$$ -> Record Start